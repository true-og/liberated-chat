plugins {
    id "java"
    id "com.modrinth.minotaur" version "2.+"
}

repositories {
    mavenCentral()
    maven {
        name = "spigot-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.21-R0.1-SNAPSHOT"
}

def pluginVersion = "1.2.4"

base.archivesName = "liberated-chat-" + pluginVersion

def javaVersion = JavaVersion.VERSION_17

java {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

jar.from("LICENSE.txt")

processResources {
	inputs.property("pluginVersion", pluginVersion)
	filesMatching("plugin.yml") {
		expand "pluginVersion": pluginVersion
	}
}

modrinth {
    token = System.getenv("MINOTAUR_PAT")
    syncBodyFrom = rootProject.file("MODRINTH_README.md").text
    uploadFile = jar
    projectId = "liberated-chat"
    changelog = rootProject.file("MODRINTH_CHANGELOG.md").text
    versionName = "Liberated Chat " + pluginVersion
    versionNumber = pluginVersion
    gameVersions = ["1.19", "1.19.1", "1.19.2", "1.19.3", "1.19.4", "1.20", "1.20.1", "1.20.2", "1.20.3", "1.20.4", "1.20.5", "1.20.6", "1.21"]
    loaders = ["bukkit", "paper", "purpur", "spigot"]
}

task modrinthUpload {
    dependsOn "modrinth"
    dependsOn "modrinthSyncBody"
}
